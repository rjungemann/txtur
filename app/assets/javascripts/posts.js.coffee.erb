<%
  def facebook_config
    YAML.load_file(Rails.root.join 'config/facebook.yml')[Rails.env]
  end
%>

$ ->
  $('abbr.timeago').timeago()

  $('.signin').click (e) ->
    e.preventDefault()

    loginCallback = (response) ->
      if response.authResponse
        window.location.reload()
      else
        console.log 'User cancelled login or did not fully authorize.'

    FB.login loginCallback, scope: 'publish_stream'

  $('.signout').click (e) ->
    e.preventDefault()

    FB.logout (response) ->
      window.location.reload()

  $('.share').click (e) ->
    e.preventDefault()

    $(this).hide()
    $('#spinner').show()

    request = $.post $(this).attr('href'), { message: '' }, (data) =>
      console.log 'Posted to Facebook', data

      $('#spinner').hide()
      $(this).replaceWith '(Shared!)'
      $(this).show()

    request.error =>
      $('#spinner').hide()
      $(this).replaceWith '(Sorry, an error occurred!)'
      $(this).show()

window.fbAsyncInit = ->
  FB.init
    appId: <%= facebook_config['app_id'] %>
    status: true
    cookie: true
    xfbml: true

(
  (d, debug) ->
    js  = undefined
    id  = 'facebook-jssdk'
    ref = d.getElementsByTagName('script')[0]

    return  if d.getElementById id

    js       = d.createElement 'script'
    js.id    = id
    js.async = true
    js.src   = '//connect.facebook.net/en_US/all' +
      ((if debug then '/debug' else '')) + '.js'

    ref.parentNode.insertBefore js, ref
) document, false

